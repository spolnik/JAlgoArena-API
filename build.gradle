buildscript {
    ext {
        ext.kotlin_version = '1.2.31'
        ext.spring_boot_version = '1.5.10.RELEASE'
    }

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${spring_boot_version}")
    }
}

apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

apply from: "$rootDir/gradle/versioning.gradle"

repositories {
    mavenCentral()
}

jar {
    baseName = 'jalgoarena-api'
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version",
            'org.springframework.boot:spring-boot-starter-web',
            'org.springframework.boot:spring-boot-starter-actuator',
            'org.springframework.cloud:spring-cloud-starter-zuul',
            'org.springframework.cloud:spring-cloud-starter-eureka'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Edgware.SR3"
    }
}

defaultTasks 'clean', 'bootRepackage'

task stage {
    dependsOn 'bootRepackage'
}

task releaseZip(type: Zip, dependsOn: 'bootRepackage') {
    from('build/libs/') {
        include('*.jar')
    }
    from 'run.sh'
    archiveName "JAlgoArena-API-${version}.zip"
    destinationDir(file('build/libs'))
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task buildDockerImage(type: Exec, dependsOn: 'bootRepackage') {
    commandLine 'docker',  'build', '-t', "spolnik/${rootProject.name}:latest", '-t', "spolnik/${rootProject.name}:${version}", '.'
}

task runAsContainer(type: Exec, dependsOn: 'buildDockerImage') {
    commandLine 'docker', 'container', 'run', '-d', '-p', '5001:5001', "spolnik/${rootProject.name}:${version}"
}